//***********************************************************************************
//Universidad del Valle de Guatemala
//BE3029 - Electrónica Digital 2
//Dulce Nicole Monney Paiz, 21549
//Laboratorio 8 - Manejo de memoria SD
//***********************************************************************************

//Librerías
#include <SD.h>
//***********************************************************************************

//Definición de pines
#define CS_PIN 5 
//***********************************************************************************

//Variables globales 
File root; //indicador del directorio raíz de la SD
//***********************************************************************************

//Prototipos de funciones
void printDirectory(File dir, int numTabs) { //enumeración del contenido del directorio del SD
  while (true) {
    File entry = dir.openNextFile();
    if (!entry) {
      //no más archivos
      break;
    }
    for (uint8_t i = 0; i < numTabs; i++) {
      Serial.print('\t');
    }
    Serial.print(entry.name());
    if (entry.isDirectory()) {
      Serial.println("/");
      printDirectory(entry, numTabs + 1);
    } else {
      //Los archivos tienen tamaños, los directorios no.
      Serial.print("\t\t");
      Serial.println(entry.size(), DEC);
    }
    entry.close();
  }
}

void showMenu() {
  Serial.println("Ingresa tu opción (1-5):");
  Serial.println("1. Mostrar imagen 1");
  Serial.println("2. Mostrar imagen 2");
  Serial.println("3. Mostrar imagen 3");
  Serial.println("4. Crear una nueva imagen");
  Serial.println("5. Ver imagen creada");
}

//Se encarga de mostrar el contenido de un archivo de imagen ubicado en una tarjeta SD.
void displayImage(const char* imagePath) { //Este parámetro es una cadena que representa la ruta al archivo de imagen en la tarjeta SD.
  File imageFile = SD.open(imagePath); 
  if (imageFile) {
    Serial.print(imagePath);
    Serial.println(": ");
    while (imageFile.available()) {
      Serial.write(imageFile.read());
    }
    imageFile.close();
  } else {
    Serial.print("Error al abrir ");
    Serial.println(imagePath);
  }
}

String readLine() { //Lectura que lo ingresado al monitor serial 
  String received = "";
  char x; //Variable almacenadora de lectura
  while (true) {
    if (Serial.available()) {
      x = Serial.read();
      if (x == '\n') {
        break;
      } else {
        received += x;
      }
    }
  }
  return received;
}

void createImage() {
  String imageData; //Acumula los datos que se escriben para dibujar la imagen
  String imageName; 
  imageName = "holasoynuevo";

  Serial.println("Ingresa el contenido de la nueva imagen. Cuando hayas terminado, escribe el comando 'FIN' en una nueva línea:");
  while (true) {
    String line = readLine();
    if (line == "FIN") {
      break;
    }
    imageData += line + "\n"; // Añade un salto de línea después de cada línea ingresada.
  }

  // Guardar los datos en la tarjeta SD
  File newImage = SD.open("/" + imageName + ".txt", FILE_WRITE); //Ruta del archivo concatenada con el nombre de la imagen y especificación de que el archivo debe abrirse para escribir
  if (newImage) { 
    newImage.print(imageData);
    newImage.close(); //Este paso es importante para guardar los cambios y liberar los recursos asociados con el archivo.
    Serial.println("Imagen creada y guardada con éxito!");
    displayImage("/holasoynuevo.txt");
  } else {
    Serial.println("Error al guardar la imagen.");
  }
}
//***********************************************************************************

//Configuración
void setup() {
  Serial.begin(115200);

  Serial.print("Inicializando la tarjeta SD...");

  if (!SD.begin(CS_PIN)) {
    Serial.println("¡Falló la inicialización de la tarjeta!D:");
    while (true);
  }

  Serial.println("Inicialización realizada:D");

  Serial.println("Archivos en la tarjeta SD:");
  root = SD.open("/");
  printDirectory(root, 0);
  Serial.println("");

  showMenu();
}
//***********************************************************************************

//Loop principal
void loop() {
  if (Serial.available()) {
    char option = Serial.read();
    switch (option) {
      case '1':
        displayImage("/DINO.txt");
        Serial.println("");
        Serial.println("");
        break;
      case '2':
        displayImage("/ScottPilgrim.txt");
        Serial.println("");
        Serial.println("");
        break;
      case '3':
        displayImage("/sleepykirby.txt");
        Serial.println("");
        Serial.println("");
        break;
      case '4':
        Serial.println("");
        Serial.println("");
        createImage();
        showMenu();
        break;
      case '5':
        displayImage("/holasoynuevo.txt");
        Serial.println("");
        Serial.println("");
        root = SD.open("/");
        printDirectory(root, 0);
        Serial.println("");
        break;
      default:
        showMenu();
        break;
    }
  }
}
//***********************************************************************************
